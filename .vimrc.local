" Neovim config to display command in a split window
"
if has('nvim')
    set inccommand=split
endif

set cursorline " highlight current line
let test#strategy = "dispatch"
" let test#filename_modifier = ":p"

" Fix issue with tmuxnavigator and tmp files
" https://github.com/christoomey/vim-tmux-navigator/issues/105
let $TMPDIR = $HOME."/tmp"

" Mappings for vim-test
nmap <leader>tn :TestNearest<CR>
nmap <leader>tf :TestFile<CR>
nmap <leader>ts :TestSuite<CR>
nmap <leader>tl :TestLast<CR>
nmap <leader>tv :TestVisit<CR>

" Move selected lines around
vnoremap J :m '>+1<cr>gv=gv
vnoremap K :m '<-2<cr>gv=gv

" Vim format mappings
let g:mix_format_on_save = 1
let g:mix_format_silent_errors = 1

" highlight search
set hlsearch

" Remove search highlight
function! s:clear_highlight()
  let @/ = ""
  call go#guru#ClearSameIds()
endfunction
nnoremap <silent> <leader>. :<C-u>call <SID>clear_highlight()<CR>

" Fast saving
nmap <leader>w :w!<cr>

" Search mappings: These will make it so that going to the next one in a
" search will center on the line it's found in.
nnoremap n nzzzv
nnoremap N Nzzzv

" Same when moving up and down
noremap <C-d> <C-d>zz
noremap <C-u> <C-u>zz

" Remap H and L (top, bottom of screen to left and right end of line)
nnoremap H ^
nnoremap L $
vnoremap H ^
vnoremap L g_

" Act like D and C
nnoremap Y y$

" Don't move on * I'd use a function for this but Vim clobbers the last search
" when you're in a function so fuck it, practicality beats purity.
nnoremap <silent> * :let stay_star_view = winsaveview()<cr>*:call winrestview(stay_star_view)<cr>

" mimic the behavior of /%Vfoobar which searches within the previously
" selected visual selection
" while in search mode, pressing / will do this
vnoremap / <Esc>/\%><C-R>=line("'<")-1<CR>l\%<<C-R>=line("'>")+1<CR>l
vnoremap ? <Esc>?\%><C-R>=line("'<")-1<CR>l\%<<C-R>=line("'>")+1<CR>l

set autowrite
set nowritebackup
set whichwrap+=<,>,h,l,[,] " Wrap arrow keys between lines

" This allows to switch tmux windows vertically from NerdTree
" Bug documented here: https://github.com/christoomey/vim-tmux-navigator/issues/83
let g:NERDTreeMapJumpNextSibling = '<Nop>'
let g:NERDTreeMapJumpPrevSibling = '<Nop>'
" Let NerdTree show hidden files
let NERDTreeShowHidden=1
" don't show .git
let NERDTreeIgnore=['\.git$']
" Config win size
let g:NERDTreeAutoDeleteBuffer=1
let g:NERDTreeMinimalUI = 1
" add keepopen to default options so stays open when opening a file
let NERDTreeCustomOpenArgs = {'file': {'reuse': 'all', 'where': 'p', 'keepopen': 1 }, 'dir': {}}
" open NerdTree on StartUp
function! StartUp()
  if 0 == argc()
    NERDTree
  end
endfunction

autocmd VimEnter * call StartUp()

" FZF {{{
" exclude .gitignore files plus .git folder and add hidden files
let $FZF_DEFAULT_COMMAND = 'rg --hidden -g "!.git" -l ""'
let $FZF_DEFAULT_OPTS = '--bind ctrl-a:select-all'

" enable history, so we can use ctrl-p and ctrl-n to see them
let g:fzf_history_dir = '~/.local/share/fzf-history'

" mappings
nnoremap <silent> <C-p> :FZF -m<CR>
nnoremap <silent> <Leader><space> :Buffers<CR>
nnoremap <silent> <Leader>h :History<CR>
nnoremap <silent> <Leader>L :Lines<CR>
inoremap <c-l> <c-\><c-o>:Snippets<CR>
nnoremap <silent> <Leader>` :Marks<CR>

" FZF }}}

" TagBar: Adapt for elixir
let g:tagbar_type_elixir = {
    \ 'ctagstype' : 'elixir',
    \ 'kinds' : [
        \ 'p:protocols',
        \ 'm:modules',
        \ 'e:exceptions',
        \ 'y:types',
        \ 'd:delegates',
        \ 'f:functions',
        \ 'c:callbacks',
        \ 'a:macros',
        \ 't:tests',
        \ 'i:implementations',
        \ 'o:operators',
        \ 'r:records'
    \ ],
    \ 'sro' : '.',
    \ 'kind2scope' : {
        \ 'p' : 'protocol',
        \ 'm' : 'module'
    \ },
    \ 'scope2kind' : {
        \ 'protocol' : 'p',
        \ 'module' : 'm'
    \ },
    \ 'sort' : 0
\ }

set shortmess+=c   " Shut off completion messages
set belloff+=ctrlg " If Vim beeps during completion

" increase max memory to show syntax highlighting for large files 
set maxmempattern=20000

" persistent vim history on disk
" Put plugins and dictionaries in this dir (also on Windows)
let vimDir = '$HOME/.vim'
let &runtimepath.=','.vimDir

" Keep undo history across sessions by storing it in a file
if has('persistent_undo')
    let myUndoDir = expand(vimDir . '/undodir')
    " Create dirs
    call system('mkdir ' . vimDir)
    call system('mkdir ' . myUndoDir)
    let &undodir = myUndoDir
    set undofile
endif

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Autocompletion
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" don't automatically show dictionary words for autocompletion " trigger with ctrlp
set complete+=kspell
set completeopt=menuone,longest " configure dictionary in english
set spelllang=en_us
set nospell
" Don't update status bar in autocompletion
set shortmess+=c
" Toggle spell check.
map <F6> :setlocal spell!<CR>

" Quickfix window {{{
function! ToggleQuickFix()
  if empty(filter(getwininfo(), 'v:val.quickfix'))
    copen
  else
    cclose
  endif
endfunction

nnoremap <silent> <leader>c :call ToggleQuickFix()<cr>

" Print full path
map <C-f> :echo expand("%:p")<cr>

" Toggle paste mode
set pastetoggle=<F5>
" Unset paste on InsertLeave.
autocmd InsertLeave * silent! set nopaste

" Swap begin of line keys for easy acces
noremap 0 ^
noremap ^ 0

" vim split on the right always
set splitright

" format/indent whole file keeping cursor at the same position
noremap =a gg=G<C-o><C-o>

" Keep cursor at the bottom of the visual selection after you yank it.
vmap y ygv<Esc>

" Prevent x from overriding what's in the clipboard.
noremap x "_x
noremap X "_x

"http://stackoverflow.com/questions/20186975/vim-mac-how-to-copy-to-clipboard-without-pbcopy
set clipboard^=unnamed
set clipboard^=unnamedplus

" -----------------------------------------------------------------------------
" Color settings
" -----------------------------------------------------------------------------
"
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" => lightline
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" let g:lightline#ale#indicator_checking = "\uf110"
" let g:lightline#ale#indicator_infos = "\uf129"
" let g:lightline#ale#indicator_warnings = "\uf071"
" let g:lightline#ale#indicator_errors = "\uf05e"
" let g:lightline#ale#indicator_ok = "\uf00c"

let g:lightline = {
      \ 'colorscheme': 'one',
      \ 'active': {
      \   'left': [ ['mode', 'paste'],
      \             ['fugitive', 'relativepath', 'modified'] ],
      \   'right': [ [ 'lineinfo' ], ['percent'] ]
      \ },
      \ 'component': {
      \   'modified': '%{&filetype=="help"?"":&modified?"+":&modifiable?"":"-"}',
      \   'fugitive': '%{exists("*FugitiveHead")?FugitiveHead():""}'
      \ },
      \ 'component_visible_condition': {
      \   'modified': '(&filetype!="help"&&(&modified||!&modifiable))',
      \   'fugitive': '(exists("*FugitiveHead") && ""!=FugitiveHead())'
      \ },
      \ 'separator': { 'left': ' ', 'right': ' ' },
      \ 'subseparator': { 'left': ' ', 'right': ' ' }
      \ }

let g:lightline.component_expand = {
      \  'linter_checking': 'lightline#ale#checking',
      \  'linter_infos': 'lightline#ale#infos',
      \  'linter_warnings': 'lightline#ale#warnings',
      \  'linter_errors': 'lightline#ale#errors',
      \  'linter_ok': 'lightline#ale#ok',
      \ }

let g:lightline.component_type = {
      \     'linter_checking': 'right',
      \     'linter_infos': 'right',
      \     'linter_warnings': 'warning',
      \     'linter_errors': 'error',
      \     'linter_ok': 'right', 
      \ }

let g:lightline.active = {    
      \   'left': [ ['mode', 'paste'], ['fugitive', 'relativepath', 'modified'] ],
      \   'right': [ [ 'lineinfo' ], ['percent'], [ 'linter_checking', 'linter_errors', 'linter_warnings', 'linter_infos', 'linter_ok' ]] 
      \ }

"
" Instead of syntax on, use This
" to avoid problems with devicons
" https://github.com/ryanoasis/vim-devicons/issues/215#issuecomment-377782464
if !exists('g:syntax_on')
  syntax enable
endif

" Enable 24-bit true colors if your terminal supports it.
if (has("termguicolors"))
  " https://github.com/vim/vim/issues/993#issuecomment-255651605
  let &t_8f = "\<Esc>[38;2;%lu;%lu;%lum"
  let &t_8b = "\<Esc>[48;2;%lu;%lu;%lum"

  set termguicolors
endif

" Set the color scheme to dark.
set background=dark

colorscheme one

" Remove background color for panels vertical split separator
hi VertSplit ctermbg=NONE guibg=NONE

" """""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" QuickScope
" Trigger a highlight in the appropriate direction when pressing these keys.
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
let g:qs_highlight_on_keys=['f', 'F', 't', 'T']

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" delimitMate config for auto close quotes
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
let g:delimitMate_expand_cr = 1   
let g:delimitMate_expand_space = 1    
let g:delimitMate_smart_quotes = 1    
let g:delimitMate_expand_inside_quotes = 0    
let g:delimitMate_smart_matchpairs = '^\%(\w\|\$\)'   

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Completion + Snippet
" Ultisnips has native support for Superab.
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" loop forward in the results list
let g:UltiSnipsExpandTrigger="<C-j>"
let g:UltiSnipsJumpForwardTrigger="<tab>"  
let g:UltiSnipsJumpBackwardTrigger="<s-tab>" 

" ExpandSnippet: returns 1 if succeded 0 otherwise
let g:ulti_expand_res = 0 
function! ExpandSnippet()
  call UltiSnips#ExpandSnippet()
  return g:ulti_expand_res
endfunction

" make nvim work with python and ruby
let g:python3_host_prog='/usr/local/bin/python3'
let g:python_host_prog='/usr/bin/python2'
let g:ruby_host_prog='/Users/pabloa/.rvm/rubies/ruby-2.6.3/bin/ruby'

" Open tag under cursor in vsplit
noremap <C-W>] :vsp <CR>:exec("tag ".expand("<cword>"))<CR>

" arrow keys resize windows
nnoremap <Left> :vertical resize -10<CR>
nnoremap <Right> :vertical resize +10<CR>
nnoremap <Up> :resize -10<CR>
nnoremap <Down> :resize +10<CR>
 
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Vim Visual Multi
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
let g:VM_maps = {}
let g:VM_maps["Add Cursor Down"]    = ',j'
let g:VM_maps["Add Cursor Up"]      = ',k'

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Vim-Fugitive
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
let g:fugitive_gitlab_domains = ['https://gitlab.otters.xyz', 'https://gitlab.com']
nmap <silent> <leader>-  :Gstatus<cr><c-n>
nnoremap <leader>gp :Gpull<cr>
nnoremap <silent> <leader>gb :Gblame<cr>
nnoremap <silent> <leader>gd :Gvdiff<cr>
nnoremap <silent> <leader>go :Gbrowse<cr>
xnoremap <silent> <leader>go :Gbrowse<cr>
nnoremap <silent> <leader>gl :0Glog<cr>
" }}} fugitive

" """""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" GITLAB CLI: https://github.com/lighttiger2505/lab
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
command! -nargs=0 Gmr :call system('lab mr -b')
nnoremap <silent> <leader>gmr :Gmr<cr>

" Run commands in ex mode and put result in quickfix
command -nargs=+ Run :cexpr system('<args>') | copen
"
" Open def in vertical/horizontal split
nnoremap <leader>v <C-w>v
"
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" vim-go 
" run :GoBuild or :GoTestCompile based on the go file
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
function! s:build_go_files()
  let l:file = expand('%')
  if l:file =~# '^\f\+_test\.go$'
    call go#test#Test(0, 1)
  elseif l:file =~# '^\f\+\.go$'
    call go#cmd#Build(0)
  endif
endfunction

let g:go_test_timeout= '3600s'

nmap <C-g> :GoDecls<cr>

" create a go doc comment based on the word under the cursor
function! s:create_go_doc_comment()
  norm "zyiw
  execute ":put! z"
  execute ":norm I// \<Esc>$"
endfunction
nnoremap <leader>ui :<C-u>call <SID>create_go_doc_comment()<CR>

augroup go
autocmd!
au FileType go nmap <silent> <leader>r <Plug>(go-run)
au FileType go nmap <silent> <leader>e <Plug>(go-install)
au FileType go nmap <silent> <leader>i <Plug>(go-info)
au FileType go nmap <silent> <leader>l <Plug>(go-metalinter)
au FileType go nmap <silent> <leader>v <Plug>(go-def-vertical)
au FileType go nmap <silent> <leader>x <Plug>(go-doc-vertical)
au FileType go nmap <silent> <leader>tc <Plug>(go-coverage-toggle)
au FileType go nmap <silent> <leader>b :<C-u>call <SID>build_go_files()<CR>
" Alternate between test and implementation file
au FileType go nmap <silent> <leader>ta :A<CR>
au FileType go nmap <silent> <leader>gc :GoCallers<CR>
au FileType go nmap <silent> <leader>ge :GoCallees<CR>
au FileType go nmap <silent> <leader>gi :GoImplements<CR>
" Find and open respective test go file
au FileType go command! -bang A call go#alternate#Switch(<bang>0, 'edit')
au FileType go command! -bang AV call go#alternate#Switch(<bang>0, 'vsplit')
au FileType go command! -bang AS call go#alternate#Switch(<bang>0, 'split')
au FileType go command! -bang AT call go#alternate#Switch(<bang>0, 'tabe')
augroup END


" Indent-guides disable mappings
let g:indent_guides_default_mapping = 0

" Add go special tab identitation
augroup filetypedetect
  autocmd FileType help wincmd L

  autocmd BufNewFile,BufRead .tmux.conf*,tmux.conf* setf tmux
  autocmd BufNewFile,BufRead .nginx.conf*,nginx.conf* setf nginx
  autocmd BufNewFile,BufRead *.hcl setf conf

  autocmd BufRead,BufNewFile *.gotmpl set filetype=gotexttmpl

  autocmd BufNewFile,BufRead *.ino setlocal noet ts=4 sw=4 sts=4
  autocmd BufNewFile,BufRead *.txt setlocal noet ts=4 sw=4
  autocmd BufNewFile,BufRead *.md setlocal noet ts=4 sw=4
  autocmd BufNewFile,BufRead *.html setlocal noet ts=4 sw=4
  autocmd BufNewFile,BufRead *.vim setlocal expandtab shiftwidth=2 tabstop=2
  autocmd BufNewFile,BufRead *.hcl setlocal expandtab shiftwidth=2 tabstop=2
  autocmd BufNewFile,BufRead *.sh setlocal expandtab shiftwidth=2 tabstop=2
  autocmd BufNewFile,BufRead *.proto setlocal expandtab shiftwidth=2 tabstop=2

  autocmd FileType go setlocal noexpandtab tabstop=4 shiftwidth=4
  autocmd FileType yaml setlocal expandtab shiftwidth=2 tabstop=2
  autocmd FileType json setlocal expandtab shiftwidth=2 tabstop=2
  autocmd FileType ruby setlocal expandtab shiftwidth=2 tabstop=2
augroup END

" use terminal buffer for console in neovim
let g:go_term_enabled = 1
let g:go_term_mode = "split above"

let g:go_fmt_fail_silently = 1
let g:go_debug_windows = {
      \ 'vars':  'leftabove 35vnew',
      \ 'stack': 'botright 10new',
\ }

let g:go_fmt_command = "goimports"
let g:go_imports_autosave=1

let g:go_test_prepend_name = 1
let g:go_list_type = "quickfix"
let g:go_term_close_on_exit = 1
let g:go_auto_type_info = 0
let g:go_auto_sameids = 0

let g:go_null_module_warning = 0
let g:go_echo_command_info = 1

let g:go_autodetect_gopath = 0

let g:go_info_mode = 'gopls'
let g:go_rename_command='gopls'
let g:go_gopls_complete_unimported = 1
let g:go_implements_mode='gopls'
let g:go_diagnostics_enabled = 1
let g:go_doc_popup_window = 1
let g:go_diagnostics_level = 2

let g:go_highlight_space_tab_error = 0
let g:go_highlight_array_whitespace_error = 0
let g:go_highlight_trailing_whitespace_error = 0
let g:go_highlight_extra_types = 0
let g:go_highlight_build_constraints = 1
let g:go_highlight_types = 1
let g:go_highlight_operators = 1
let g:go_highlight_format_strings = 0
let g:go_highlight_function_calls = 0
let g:go_highlight_function_parameters = 0
let g:go_gocode_propose_source = 1
let g:go_highlight_variable_declarations = 1
let g:go_highlight_variable_assignments = 1

let g:go_modifytags_transform = 'camelcase'
let g:go_fold_enable = []

let g:go_test_show_name = 1

let g:go_metalinter_autosave = 0
let g:go_metalinter_autosave_enabled = ['govet', 'golint']
let g:go_metalinter_command = "golangci-lint run --disable-all -e composites -E goimports -E golint -E govet -E ineffassign -E rowserrcheck"

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" => FZF
" Allow to pass commands to RG
" Search by default hidden files except .git folder
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
command! -bang -nargs=* Rg
  \ call fzf#vim#grep(
  \ "rg --column --line-number --no-heading --color=always --hidden --smart-case " .
  \ <q-args>, 1, fzf#vim#with_preview(), <bang>0)

" Use fzf Snippets to search through description not only name
command! -bar -bang Snippets call fzf#vim#snippets({'options': '--tiebreak=begin -n ..'}, <bang>0)

" ~/.viminfo needs to be writable and readable. Set oldfiles to 1000 last
" recently opened files, :FzfHistory uses it
set viminfo='1000

" Time out on key codes but not mappings.
" Basically this makes terminal Vim work sanely.
if !has('gui_running')
  set notimeout
  set ttimeout
  set ttimeoutlen=10
  augroup FastEscape
    autocmd!
    au InsertEnter * set timeoutlen=0
    au InsertLeave * set timeoutlen=1000
  augroup END
endif

" Empty value to disable preview window altogether
let g:fzf_preview_window = ''

" Customize fzf colors to match your color scheme.
let g:fzf_colors =
      \ { 'fg':    ['fg', 'Normal'],
      \ 'bg':      ['bg', 'Normal'],
      \ 'hl':      ['fg', 'Comment'],
      \ 'fg+':     ['fg', 'CursorLine', 'CursorColumn', 'Normal'],
      \ 'bg+':     ['bg', 'CursorLine', 'CursorColumn'],
      \ 'hl+':     ['fg', 'Statement'],
      \ 'info':    ['fg', 'PreProc'],
      \ 'border':  ['fg', 'Ignore'],
      \ 'prompt':  ['fg', 'Conditional'],
      \ 'pointer': ['fg', 'Exception'],
      \ 'marker':  ['fg', 'Keyword'],
      \ 'spinner': ['fg', 'Label'],
      \ 'header':  ['fg', 'Comment'] }

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" => Moving around, tabs, windows and buffers
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Return to last edit position when opening files (You want this!)
au BufReadPost * if line("'\"") > 1 && line("'\"") <= line("$") | exe "normal! g'\"" | endif

" Close the current buffer
map <leader>Bd :Bclose<cr>:tabclose<cr>gT

" Super useful when editing files in the same directory
map <leader>te :tabedit <C-r>=expand("%:p:h")<cr>/

" Search word under the cursor across directory
nnoremap <silent> <leader>sw       :Rg <C-R><C-W><CR>
nnoremap <silent> <leader>SW       :Rg <C-R><C-A><CR>
xnoremap <silent> <leader>sw       y:Rg <C-R>"<CR>

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Smart mappings on the command line
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
cno $c e <C-\>eCurrentFileDir("e")<cr>

" Bash like keys for the command line
cnoremap <C-A> <Home>
cnoremap <C-E> <End>
cnoremap <C-K> <C-U>

func! CurrentFileDir(cmd)
  return a:cmd . " " . expand("%:p:h") . "/"
endfunc

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" => Ale (syntax checker and linter)
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
nmap <silent> <leader>n <Plug>(ale_next_wrap)

let g:ale_linters = { 'go': ['golangci-lint'], 'proto': ['protolint'] }
let g:ale_fixers = { 'proto': ['protolint'] }
let g:ale_proto_protolint_config = "/Users/pabloa/.protolint.yml"
let g:ale_go_golangci_lint_package = 1
let g:ale_go_golangci_lint_options = '--disable-all -e composites -E goimports -E golint -E govet -E ineffassign -E rowserrcheck'

" ----------------------------------------------------------------------------
" :Root | Change directory to the root of the Git repository
" from https://github.com/junegunn/dotfiles/blob/master/vimrc#L657
"  ----------------------------------------------------------------------------
function! s:root()
  let root = systemlist('git rev-parse --show-toplevel')[0]
  if v:shell_error
    echo 'Not in git repo'
  else
    execute 'lcd' root
    echo 'Changed directory to: '.root
  endif
endfunction
command! Root call s:root()

" """"""""""""""""""""""""""""""
"
" Visual linewise up and down by default (and use gj gk to go quicker)
noremap j gj
noremap k gk

" Exit on j
imap jj <Esc>

" Center the screen
nnoremap <space> zz

" Profile Vim by running this command once to start it and again to stop it.
function! s:profile(bang)
  if a:bang
    profile pause
    noautocmd qall
  else
    profile start /tmp/profile.log
    profile func *
    profile file *
  endif
endfunction

command! -bang Profile call s:profile(<bang>0)

" show split window always at the very bottom of all other windows
:autocmd FileType qf wincmd J
